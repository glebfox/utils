/*
 * Copyright (c) 2016-2020 Gleb Gorelov.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'com.github.spotbugs' version '4.5.0'
    id 'com.github.kt3k.coveralls' version '2.10.2'
}

ext {
    artifactGroup = 'com.glebfox'
    artifactName = 'util'
    artifactVersion = '2.0.0-SNAPSHOT'
    artifactVersion = rootProject.hasProperty('buildVersion') ? rootProject['buildVersion'] : artifactVersion
}

group = artifactGroup
version = artifactVersion

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'utils'
            from components.java

            pom {
                name = 'utils'
                description = 'A collection of Java utility classes, that may be helpful in everyday programming.'
                url = 'https://glebfox.github.io/utils/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'glebfox'
                        name = 'Gleb Gorelov'
                        email = 'glebfox@gmail.com'
                        url = 'glebfox.com'
                    }
                }

                scm {
                    connection = 'scm:https://github.com/glebfox/utils.git'
                    developerConnection = 'scm:https://github.com/glebfox/utils.git'
                    url = 'https://github.com/glebfox/utils/'
                }
            }
        }
    }

    if (rootProject.hasProperty('uploadUser') && rootProject.hasProperty('uploadPassword')) {
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = rootProject['uploadUser']
                    password = rootProject['uploadPassword']
                }
            }
        }
    }
}

if (rootProject.hasProperty('useSigning')) {
    signing {
        sign publishing.publications.mavenJava
    }
}

spotbugs {
    effort = 'max'
    excludeFilter = rootProject.file("codestyle/exclude-filter.xml")
}

spotbugsMain {
    reports {
        xml.enabled = false
        html {
            enabled = true
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    reports {
        xml.enabled = false
        html {
            enabled = true
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
